apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'

buildscript {
    repositories {
        jcenter()
    }
}

android {
    // Please update the ".travis.yml" file "android.components" section
    compileSdkVersion targetSdk
    buildToolsVersion buildTools

    defaultConfig {
        applicationId "com.vimeo.sample"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        stagGeneratedPackageName: 'com.vimeo.sample.stag.generated',
                        stagDebug               : 'true'
                ]
            }
        }
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }
}

jacoco {
    toolVersion = jacoco_version
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:25.3.1'

    implementation project(':sample-model')
    implementation project(':sample-java-model')

    implementation(project(':sample-kotlin-model')) {
        exclude group: 'org.jetbrains', module: 'annotations'
    }

    implementation project(':stag-library')
    annotationProcessor project(':stag-library-compiler')

    implementation 'com.google.code.gson:gson:2.8.0'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs += [
                "-Xlint:all,-deprecation,-serial,-processing",
                "-Werror"
        ]
    }
}
